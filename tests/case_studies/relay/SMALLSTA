/* bedrijfstoestandskeuze systeem IRM */
/* Peter van Eijk Sept 93 */
/* deze versie laat alleen maar bediening toe als het systeem stabiel is */

#define __instances_tks 1
#define __instances_bediening 1
#define __instances_checks 1

bool k14_1, s1_1, k12_1, h14_1;
bool k14_2, s1_2, k12_2, h14_2;
bool dienstv;
bool stable;

proctype tks()
{
do
::	k14_1 = s1_1 && !k12_1; skip
::	k12_1 = dienstv && (!s1_1 || k12_1); skip

::	k14_2 = s1_2 && !k12_2; skip
::	k12_2 = dienstv && (!s1_2 || k12_2); skip

::	dienstv = k14_1 || k14_2; skip

::	stable=
	(k14_1 == (s1_1 && !k12_1)) &&
	(k12_1 == (dienstv && (!s1_1 || k12_1))) &&
	(k14_2 == (s1_2 && !k12_2)) &&
	(k12_2 == (dienstv && (!s1_2 || k12_2))) &&
	(dienstv == (k14_1 || k14_2));  stable=0
od
}

proctype bediening()
{
do
::	atomic{ stable; s1_1=1; stable=0}
::	atomic{ stable; s1_1=0; stable=0}

::	atomic{ stable; s1_2=1; stable=0}
::	atomic{ stable; s1_2=0; stable=0}

od
}

proctype checks()
{
atomic{	stable;
	assert(!(k14_1 && k14_2)); /* niet twee keer dienstv lokaal */ 
	assert(!(k14_1 && k12_1)) /* niet lokaal en niet lokaal */
}
}

init
{	run tks();
	run bediening();
	run checks()
}


